plugins {
  id 'java'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'com.github.spotbugs' version '5.0.6'
  id 'io.freefair.lombok' version '6.5.1'
  id 'org.springframework.boot' version '2.6.7'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

bootJar {
  archiveBaseName = 'alfred'
}

group = 'de.wasenweg'
sourceCompatibility = 17.0
targetCompatibility = 17.0

repositories {
  mavenCentral()
  google()
}

checkstyle {
  toolVersion '10.3.4'
}

spotbugs {
  effort = 'max'
}

pmd {
  consoleOutput = true
  toolVersion = '6.50.0'
  incrementalAnalysis = true
  reportsDir = file("pmd")
}

jacocoTestReport {
  reports {
    xml.required = true
    html.required = true
  }
}

tasks.pmdMain {
  ruleSets = []
  ruleSetConfig = resources.text.fromFile("config/pmd/pmd-main.xml")
}

tasks.pmdTest {
  ruleSets = []
  ruleSetConfig = resources.text.fromFile("config/pmd/pmd-test.xml")
}

spotbugsMain {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

test {
  useJUnitPlatform()
}

bootRun.dependsOn ':ui:jar'
bootJar.dependsOn ':ui:jar'

task unpack(type: Copy) {
  dependsOn bootJar
  from(zipTree(tasks.bootJar.outputs.files.singleFile))
  into("build/dependency")
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-data-rest'
  implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-validation'

  implementation 'com.google.api-client:google-api-client:1.30.9'
  implementation 'com.google.http-client:google-http-client-apache-v2:1.35.0'
  implementation 'org.springframework.security:spring-security-jwt:1.1.1.RELEASE'
  implementation 'com.auth0:java-jwt:3.10.3'
  implementation 'org.projectlombok:lombok'

  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation 'org.glassfish.jersey.media:jersey-media-sse'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.mockito:mockito-junit-jupiter'
  testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:3.4.11'
  testImplementation 'io.projectreactor:reactor-test:3.4.17'
  testImplementation 'org.mock-server:mockserver-netty:5.13.2'
  testImplementation 'org.mock-server:mockserver-client-java:5.13.2'
}
