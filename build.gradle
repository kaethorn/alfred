plugins {
  id 'java'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'com.github.spotbugs' version '5.0.6'
  id 'io.freefair.lombok' version '5.1.0'
  id 'org.springframework.boot' version '2.6.7'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

bootJar {
  archiveBaseName = 'alfred'
}

group = 'de.wasenweg'
sourceCompatibility = 11.0
targetCompatibility = 11.0

repositories {
  mavenCentral()
  google()
}

checkstyle {
  toolVersion '10.2'
}

spotbugs {
  effort = 'max'
}

pmd {
  consoleOutput = true
  toolVersion = '6.25.0'
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
}

tasks.pmdMain {
  ruleSets = []
  ruleSetConfig = resources.text.fromFile("config/pmd/pmd-main.xml")
}

tasks.pmdTest {
  ruleSets = []
  ruleSetConfig = resources.text.fromFile("config/pmd/pmd-test.xml")
}

spotbugsMain {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

test {
  useJUnitPlatform()
}

task unpack(type: Copy) {
  dependsOn bootJar
  from(zipTree(tasks.bootJar.outputs.files.singleFile))
  into("build/dependency")
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-data-rest'
  implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-validation'

  implementation 'com.google.api-client:google-api-client:1.30.9'
  implementation 'com.google.http-client:google-http-client-apache-v2:1.35.0'
  implementation 'org.springframework.security:spring-security-jwt:1.1.1.RELEASE'
  implementation 'com.auth0:java-jwt:3.10.3'
  implementation 'org.projectreactor:reactor-spring:1.0.1.RELEASE'
  implementation 'org.projectlombok:lombok:1.18.12'

  testImplementation('org.springframework.security:spring-security-test') {
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  testImplementation 'org.glassfish.jersey.media:jersey-media-sse:2.31'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.mockito:mockito-junit-jupiter'
  testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
  testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0'
  testImplementation 'io.projectreactor:reactor-test'
  testImplementation 'org.mock-server:mockserver-netty:5.10.0'
  testImplementation 'org.mock-server:mockserver-client-java:5.10.0'

  implementation(project(':ui'))
}
